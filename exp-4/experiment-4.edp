load "ppm2rnm"
load "isoline"
load "medit"

real[int, int] Curves(3, 1);
int[int] be(1);
real[int, int] ff1("TN3.pgm");
int nx = ff1.n, ny = ff1.m;
mesh Th = square(nx - 1, ny - 1, [ (nx - 1) * (x), (ny - 1) * (1 - y) ]);
fespace Vh(Th, P1);
Vh f1;
f1[] = ff1;
int nc = isoline(Th, f1, iso = 0.2, Curves, beginend = be);
cout << "number of isoline curves: " << nc << endl;
border C0(t = 0, 1)
{
    int c = 0;
    P = Curve(Curves, be[2 * c], be[2 * c + 1] - 1, t);
}
border C1(t = 0, 1)
{
    int c = 2;
    P = Curve(Curves, be[2 * c], be[2 * c + 1] - 1, t);
}

int n = 200;
Th = buildmesh(C0(-n) + C1(-n));

plot(Th, wait=1);
real alpha = 0.204;
real beta = 0.83;
real gamma = 0.46;
real delta = 1;
real dt = 0.2, T = 94;

real uAst = 1 - alpha + (alpha * (gamma + delta)) / beta;
real vAst = ((beta - gamma - delta) / (gamma + delta)) * uAst;

Vh u, v, wu, wv, uOld, vOld;

u = uAst;
v = vAst;

uOld = u;
vOld = v;

ofstream file("./u_v_count.txt");
int iteration = 0;
for (real t = 0; t < T; t += dt)
{
    solve Predator(v, wv) = int2d(Th)(v * wv / dt - beta * uOld * v * wv / (abs(uOld) + alpha) + gamma * v * wv) - int2d(Th)(vOld * wv / dt) + int2d(Th)(delta * (dx(v) * dx(wv) + dy(v) * dy(wv)));

    solve Prey(u, wu) = int2d(Th)(u * wu / dt - u * wu + u * abs(uOld) * wu) - int2d(Th)(uOld * wu / dt) + int2d(Th)(dx(u) * dx(wu) + dy(u) * dy(wu)) + int2d(Th)(uOld * v * wu / (abs(uOld) + alpha));

    vOld = v;
    uOld = u;
    iteration++;
    cout << "Iteration: " << iteration << endl;
    // plot(u, nbiso = 20, value = 1, fill = 1, cmm = "prey");
    plot(v, nbiso=40, value=1, fill=1, cmm="predator");

    real ucount = int2d(Th)(u);
    real vcount = int2d(Th)(v);
    file << t << " ; " << ucount << " ; " << vcount << endl;
}

plot(u, nbiso = 40, value = 1, fill = 1, ps = "prey.ps", cmm = "prey");
plot(v, nbiso = 40, value = 1, fill = 1, ps = "predator.ps", cmm = "predator");

file.flush;